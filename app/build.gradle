buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-android-extensions'

ext {
    supportLibraryVersion = '25.3.0'
    grpcVersion = '1.3'
    protobufVersion = '3.3.0'
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    sourceSets.main.jni.srcDirs = []
    sourceSets.main.jniLibs.srcDirs = []

    defaultConfig {
        applicationId "com.howtoandtutorial.translateapp"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 2
        versionName "1.1"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            moduleName "libWritePadRecos"
            abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
        }
    }

    signingConfigs {
        release {
            storeFile file(project.properties.storeFile)
            storePassword project.properties.storePassword
            keyAlias project.properties.keyAlias
            keyPassword project.properties.keyPassword
        }
    }

    productFlavors {
        dev {
            // Minimum version with platform multi-dex support
            minSdkVersion 21
            // Add a comment to this line
            }
        prod {
            // Minimum version that can run gRPC (TLS extension)
            minSdkVersion 16
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            multiDexEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
        resolutionStrategy.force "com.android.support:support-annotations:25.3.0"
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '25.3.0'
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/ASL2.0'
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkLibsToJar
    }

    task ndkLibsToJar(type: Zip, dependsOn: 'ndkBuild', description: 'Create a JAR of the native libs') {
        destinationDir new File(buildDir, 'libs')
        baseName 'ndk-libs'
        extension 'jar'
        from(new File(buildDir, 'libs')) { include '**/*.so' }
        into 'lib/'
    }

    task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {

        def ndkDir  // '/Android/android-ndk'
        if (android.hasProperty('plugin')) {
            if (android.plugin.hasProperty('sdkHandler')) {
                ndkDir = android.plugin.sdkHandler.getNdkFolder()
            } else {
                ndkDir = android.plugin.ndkDirectory.absolutePath
            }
        } else {
            ndkDir = android.ndkDirectory.absolutePath
        }
        // def ndkDir = plugins.getPlugin('com.android.application').sdkHandler.getNdkFolder()
        //C:/Users/Dao/AppData/Local/Android/sdk/ndk-bundle/ndk-build.cmd
        commandLine "$ndkDir\\ndk-build.cmd", 'APP_ABI=all', 'APP_OPTIM=release',
                'NDK_PROJECT_PATH=build',
                'APP_BUILD_SCRIPT=src/main/jni/Android.mk'
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile fileTree(dir: new File(buildDir, 'libs'), include: '*.jar')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile('com.google.auth:google-auth-library-oauth2-http:0.6.0') {
        exclude module: 'httpclient'
    }
    compile files('src/main/java/google-api-translate-java-0.92.jar')
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile ('com.google.api-client:google-api-client:1.21.0'){
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:design:25.3.0'
    compile 'com.android.support:recyclerview-v7:25.3.0'
    compile 'com.android.support:cardview-v7:25.3.0'
    compile 'io.grpc:grpc-okhttp:1.3.0'
    compile 'io.grpc:grpc-protobuf-lite:1.3.0'
    compile 'io.grpc:grpc-stub:1.3.0'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'com.google.protobuf:protobuf-java:3.3.1'
    compile 'com.google.api.grpc:grpc-google-cloud-speech-v1:0.1.11'
    compile 'com.google.android.gms:play-services:10.2.0'
    compile 'com.theartofdev.edmodo:android-image-cropper:2.4.+'
    compile ('com.google.apis:google-api-services-translate:v2-rev49-1.22.0'){
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile 'com.rmtheis:tess-two:6.3.0'
    testCompile 'junit:junit:4.12'
}